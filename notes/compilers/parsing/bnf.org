* Backus-Naur

** Sources

| source                       | author                        |
|------------------------------+-------------------------------|
| *Backus-Naur Form*           | Wikipedia                     |
| *Foundations of Computation* | Carol Critchlow and David Eck |

** Backus-Naur Syntax in BNF

#+begin_example
<syntax>     ::= <rule>
               | <rule> <syntax>

<rule>       ::= <spaces> "<" <rule-name> ">" <spaces> "::=" <spaces> <expression> <line-end>

<spaces>     ::= " " <spaces>
               | ""

<expression> ::= <list>
               | <list> <spaces> "|" <spaces> <expression>

<line-end>   ::= <spaces> <EOL>
               | <line-end> <line-end>

<list>       ::= <term>
               | <term> <spaces> <list>

<term>       ::= <literal>
               | "<" <rule-name> ">"

<literal>    ::= '"' <text1> '"'
               | "'" <text2> "'"

<text1>      ::= ""
               | <character1> <text1>

<text2>      ::= ""
               | <character2> <text2>

<character>  ::= <letter>
               | <digit>
               | <symbol>

<letter>     ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" |
                 "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" |
                 "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d" | "e" | "f" | "g" |
                 "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" |
                 "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"

<digit>      ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<symbol>     ::= "|" | " " | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" |
                 "," | "-" | "." | "/" | ":" | ";" | ">" | "=" | "<" | "?" | "@" |
                 "[" | "\" | "]" | "^" | "_" | "`" | "{" | "}" | "~"

<character1> ::= <character>
               | "'"

<character2> ::= <character>
               | '"'

<rule-name>  ::= <letter>
               | <rule-name> <rule-char>

<rule-char>  ::= <letter>
               | <digit>
               | "-"
#+end_example

** Backus-Naur Form to Context-Free Grammar

*** Backus-Naur Form

#+begin_example
E ::= T [ + T ] ...
T ::= F [ * F ] ...
F ::= "(" E ")" | x | y | z
#+end_example

*** Context-Free Grammar

**** LL(1)

#+begin_example
=== grammar ===

E → T A
A → + T A
A → ε
T → F B
B → * F B
B → ε
F → ( E )
F → x
F → y
F → z

=== derivation ===

E ⇒ T A
  ⇒ F B A
  ⇒ x B A
  ⇒ x A
  ⇒ x + T A
  ⇒ x + F B A
  ⇒ x + y B A
  ⇒ x + y * F B A
  ⇒ x + y * z B A
  ⇒ x + y * z A
  ⇒ x + y * z
#+end_example

**** LR(1)

#+begin_example
=== grammar ===

E → E + T
E → T
T → T * F
T → F
F → ( E )
F → x
F → y
F → z

=== derivation ===

E ⇒ T
  ⇒ T * F
  ⇒ T * z
  ⇒ F * z
  ⇒ ( E ) * z
  ⇒ ( E + T ) * z
  ⇒ ( E + F ) * z
  ⇒ ( E + y ) * z
  ⇒ ( T + y ) * z
  ⇒ ( F + y ) * z
  ⇒ ( x + y ) * z
#+end_example
